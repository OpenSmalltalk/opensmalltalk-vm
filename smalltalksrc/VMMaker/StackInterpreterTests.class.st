Class {
	#name : #StackInterpreterTests,
	#superclass : #TestCase,
	#category : 'VMMaker-Tests'
}

{ #category : #tests }
StackInterpreterTests >> testImmediateFloats [
	"self new testImmediateFloats"
	| vm smm smallFloatOop |
	vm := StackInterpreterSimulator newWithOptions: #(ObjectMemory Spur64BitMemoryManager).
	smm := vm objectMemory.
	#(0.0 -0.0 1.0e38 -1.0e38 1.0e-38 -1.0e-38 2.0 -2.0) do:
		[:f| | sfo |
		vm initPrimCall.
		self shouldnt: [sfo := smm smallFloatObjectOf: f] raise: Error.
		self assert: (smm smallFloatValueOf: sfo) equals: f.
		self assert: ((smm exponentOfSmallFloat: sfo) between: -126 and: 127)].
	{Float nan. Float infinity. Float negativeInfinity. 1.0e-39. 1.0e39. 5.8774717541114375e-39. 6.80564733841877e38 } do:
		[:f| self deny: (smm isSmallFloatValue: f)].
	vm initPrimCall.
	smallFloatOop := smm smallFloatTag + (1 << (smm smallFloatMantissaBits + smm numTagBits)).
	self shouldnt: [smm floatValueOf: smallFloatOop] raise: Error.
	self deny: vm failed.
	self assert: (smm isSmallFloatValue: (smm floatValueOf: smallFloatOop)).
	self assert: (smm floatObjectOf: (smm floatValueOf: smallFloatOop)) equals: smallFloatOop.
	vm initPrimCall.
	smallFloatOop := ((1 bitShift: 64) - 1 bitClear: (1 bitShift: smm numTagBits + 1) - 1) + smm smallFloatTag.
	self shouldnt: [smm floatValueOf: smallFloatOop] raise: Error.
	self deny: vm failed.
	self assert: (smm isSmallFloatValue: (smm floatValueOf: smallFloatOop)).
	self assert: (smm floatObjectOf: (smm floatValueOf: smallFloatOop)) equals: smallFloatOop
]

{ #category : #tests }
StackInterpreterTests >> testPointerTaggingDetagging [
	"Test the adding/removal of SmallInteger tags to memory adresses used in
	 StackInterpreter and StackInterpreterSimulator for married context management."

	"StackInterpreterTests new testPointerTaggingDetagging"

	{ StackInterpreterSimulatorLSB new. "StackInterpreterSimulatorMSB new" } do: "Need to add MSB soon"
		[:sis|
		({ SmallInteger minVal. SmallInteger minVal / 2. -1024. -4.
		    SmallInteger maxVal - 1. SmallInteger maxVal // 2. 4 } collect:
			[:n| n bitAnd: -4]) do:
				[:n| | tagged untagged |
				self assert: n \\ 4 = 0.
				self assert: (sis objectMemory isNonIntegerObject: n).
				tagged := sis withSmallIntegerTags: n.
				untagged := sis withoutSmallIntegerTags: tagged.
				self assert: (sis objectMemory isIntegerObject: tagged).
				self assert: untagged = n]]
]

{ #category : #tests }
StackInterpreterTests >> testUnalignedMemoryAccess [
	"self new testUnalignedMemoryAccess"
	| om |
	om := NewCoObjectMemorySimulator new allocateMemoryOfSize: 16.
	om unalignedLongAt: 1 put: 16r11223344.
	self assert: (om unalignedLongAt: 0) equals: 16r22334400.
	self assert: (om unalignedLongAt: 4) equals: 16r11.
	self assert: (om unalignedLongAt: 1) equals: 16r11223344.
	om longAt: 0 put: 16rAAAAAAAA.
	om longAt: 4 put: 16rAAAAAAAA.
	om unalignedLongAt: 1 put: 16r11223344.
	self assert: (om unalignedLongAt: 0) equals: 16r223344AA.
	self assert: (om unalignedLongAt: 4) equals: 16rAAAAAA11.
	self assert: (om unalignedLongAt: 1) equals: 16r11223344.
	om := Spur32BitMMLECoSimulator new allocateMemoryOfSize: 16.
	om unalignedLongAt: 3 put: 16r11223344.
	self assert: (om unalignedLongAt: 0) equals: 16r44000000.
	self assert: (om unalignedLongAt: 4) equals: 16r112233.
	self assert: (om unalignedLongAt: 3) equals: 16r11223344.
	om longAt: 0 put: 16rAAAAAAAA.
	om longAt: 4 put: 16rAAAAAAAA.
	om unalignedLongAt: 3 put: 16r11223344.
	self assert: (om unalignedLongAt: 0) equals: 16r44AAAAAA.
	self assert: (om unalignedLongAt: 4) equals: 16rAA112233.
	self assert: (om unalignedLongAt: 3) equals: 16r11223344.
	om := Spur64BitMMLECoSimulator new allocateMemoryOfSize: 32.
	om unalignedLongAt: 3 put: 16r1122334455667788.
	self assert: (om unalignedLongAt: 0) equals: 16r4455667788000000.
	self assert: (om unalignedLongAt: 8) equals: 16r112233.
	self assert: (om unalignedLongAt: 3) equals: 16r1122334455667788.
	om longAt: 0 put: 16rAAAAAAAAAAAAAAAA.
	om longAt: 8 put: 16rAAAAAAAAAAAAAAAA.
	om unalignedLongAt: 3 put: 16r1122334455667788.
	self assert: (om unalignedLongAt: 0) equals: 16r4455667788AAAAAA.
	self assert: (om unalignedLongAt: 8) equals: 16rAAAAAAAAAA112233.
	self assert: (om unalignedLongAt: 3) equals: 16r1122334455667788.
]
