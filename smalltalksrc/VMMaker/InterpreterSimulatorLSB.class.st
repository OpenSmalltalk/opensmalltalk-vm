"
This class overrides a few methods in InterpreterSimulator required for simulation to work on little-endian architectures (such as the x86 family of processors).  To start it up simply use InterpreterSimulatorLSB instead of InterpreterSimulator (see the class comment there for more details).  For example:

	(InterpreterSimulatorLSB new openOn: Smalltalk imageName) test

Note that the image must have been saved at least once on the local architecture, since the compiled VM performs some byte swapping that the simulator cannot cope with.
"
Class {
	#name : #InterpreterSimulatorLSB,
	#superclass : #InterpreterSimulator,
	#category : #'VMMaker-InterpreterSimulation'
}

{ #category : #'memory access' }
InterpreterSimulatorLSB >> byteAt: byteAddress [
	| lowBits long |
	lowBits _ byteAddress bitAnd: 3.
	long _ self longAt: byteAddress - lowBits.
	^(lowBits caseOf: {
		[0] -> [ long ].
		[1] -> [ long bitShift: -8  ].
		[2] -> [ long bitShift: -16 ].
		[3] -> [ long bitShift: -24 ]
	}) bitAnd: 16rFF

]

{ #category : #'memory access' }
InterpreterSimulatorLSB >> byteAt: byteAddress put: byte [
	| lowBits long longAddress |
	lowBits _ byteAddress bitAnd: 3.
	longAddress _ byteAddress - lowBits.
	long _ self longAt: longAddress.
	long _ (lowBits caseOf: {
		[0] -> [ (long bitAnd: 16rFFFFFF00) bitOr: byte ].
		[1] -> [ (long bitAnd: 16rFFFF00FF) bitOr: (byte bitShift: 8) ].
		[2] -> [ (long bitAnd: 16rFF00FFFF) bitOr: (byte bitShift: 16)  ].
		[3] -> [ (long bitAnd: 16r00FFFFFF) bitOr: (byte bitShift: 24)  ]
	}).

	self longAt: longAddress put: long.

]

{ #category : #'debug support' }
InterpreterSimulatorLSB >> charsOfLong: long [
	^ (1 to: 4) collect:
		[:i | ((long digitAt: i) between: 14 and: 126)
					ifTrue: [(long digitAt: i) asCharacter]
					ifFalse: [$?]]
]

{ #category : #'debug support' }
InterpreterSimulatorLSB >> displayForm: f [
	| width height depth bits realForm simDisp realDisp |
	bits _ self fetchPointer: 0 ofObject: f.
	width _ self fetchInteger: 1 ofObject: f.
	height _ self fetchInteger: 2 ofObject: f.
	depth _ self fetchInteger: 3 ofObject: f.
	realForm _ Form extent: width@height depth: depth.
	simDisp _ Form new hackBits: memory.
	realDisp _ Form new hackBits: realForm bits.
	realDisp
		copy: (0 @ 0 extent: 4 @ realForm bits size)
		from: (0 @ (bits + 4 // 4))
		in: simDisp
		rule: Form over.
	realForm displayOn: Display at: 0@0.
]

{ #category : #'memory access' }
InterpreterSimulatorLSB >> halfWordAt: byteAddress [
    "Return the half-word at byteAddress which must be even."
	| lowBits long |
	lowBits _ byteAddress bitAnd: 2.
	long _ self longAt: byteAddress - lowBits.
	^lowBits = 2
		ifTrue: [ long bitShift: -16 ]
		ifFalse: [ long bitAnd: 16rFFFF ].

]

{ #category : #'memory access' }
InterpreterSimulatorLSB >> halfWordAt: byteAddress put: a16BitValue [
    "Return the half-word at byteAddress which must be even."
	| lowBits long longAddress |
	lowBits _ byteAddress bitAnd: 2.
	lowBits = 0
		ifTrue: [ "storing into LS word"
			long _ self longAt: byteAddress.
			self longAt: byteAddress put: ((long bitAnd: 16rFFFF0000) bitOr: a16BitValue)
		]
		ifFalse: [
			longAddress _ byteAddress - 2.
			long _ self longAt: longAddress.
			self longAt: longAddress
				put: ((long bitAnd: 16rFFFF) bitOr: (a16BitValue bitShift: 16))
		]
]

{ #category : #initialization }
InterpreterSimulatorLSB >> nextLongFrom: aStream [
	"Read a 32-bit quantity from the given (binary) stream."
	| bytes |
	bytes _ aStream nextInto: (ByteArray new: 4).
	^ Integer
		byte1: (bytes at: 1)
		byte2: (bytes at: 2)
		byte3: (bytes at: 3)
		byte4: (bytes at: 4)
]

{ #category : #'image save/restore' }
InterpreterSimulatorLSB >> putLong: n toFile: f [
	"Append the given 4-byte long word to the given file in my byte order. (Bytes will be swapped, if necessary, when the image is read on a different platform.) Set successFlag to false if the write fails."

	| remainingValue |

	remainingValue _ n.
	4 timesRepeat: [
		f nextPut: (remainingValue bitAnd: 16rFF).
		remainingValue _ remainingValue bitShift: -8].

	self success: true
]
